# @param community_id
# @param course_name
# @param return_url (optional)


if {![exists_and_not_null return_url]} {
    set return_url [ad_return_url]
}

if {![exists_and_not_null scope]} {
    set scope course
}
set email_types [list "on join" "waitinglist approved" "prereq approval" "prereq reject"]
if {[parameter::get -package_id [ad_conn package_id] -parameter EnableCourseApplicationsP -default 1]} {
    lappend email_types "on approval" "awaiting payment" 
}

db_multirow -extend {edit_url} email_templates get_email_templates "select subject,type from dotlrn_member_emails where community_id=:community_id" {
    set edit_url [export_vars -base email-template {{community_id $community_id} {action $type} return_url}]
    if {[set index [lsearch $email_types $type]] > -1} {
	set email_types [lreplace $email_types $index $index]
    }
}

foreach type $email_types {
    set edit_url [export_vars -base email-template {{community_id $community_id} {action $type} return_url}]
    set email [lindex [callback dotlrn::default_member_email -community_id $community_id -type $type -var_list [list course_name $course_name]] 0]
    if {[llength $email]} {
	set subject "[lindex $email 1]"
	switch [lindex $email 3] {
	    dotlrn-ecommerce {
		append subject 	"(using site-wide default template)"
	    }
	    course {
		append subject "(using course default template)"
	    }
	    default {

	    }
	}
	template::multirow append email_templates $subject $type $edit_url
    }
}

template::list::create \
    -name email_templates \
    -multirow email_templates \
    -elements {
        subject {label "Email subject"  link_url_col edit_url}
        type {label "Type"}
    }